// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // Zeroing the x input or not can be done multiplexing between the input itself or all zeros, based on the nx and zx control bits
    Mux16(a=x, b=false, sel=zx, out=zxout);
    Not16(in=zxout, out=notzx);
    Mux16(a=zxout, b=notzx, sel=nx, out=nxout);

    // Same idea for the y bits, based on the ny and zy control bits
    Mux16(a=y, b=false, sel=zy, out=zyout);
    Not16(in=zyout, out=notny);
    Mux16(a=zyout, b=notny, sel=ny, out=nyout);
    
    // Multiplexing between X + Y and X AND Y based on the f control bit
    Add16(a=nxout, b=nyout, out=xplusy);
    And16(a=nxout, b=nyout, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=fout);

    // Multiplexes between fout and its negation based on the no control bit
    // Also subdivides the output pins into two sets of eight and its most significant bit, in order to be used later when checking if output is zero or negative
    Not16(in=fout, out=notfout);
    Mux16(a=fout, b=notfout, sel=no, out=out, out[15]=msbit, out[0..7]=lsbyte, out[8..15]=msbyte);

    // Combining the 8 least significant bits with the 8 most significant bits we can check if out is zero using Or8Way gates since they evaluate to 1 if any of its inputs is a 1, thus evaluating to zero only when all its inputs are zero
    // Finally since zr must be 1 if output is 0, all that is left is to invert the output
    Or8Way(in=lsbyte, out=least);
    Or8Way(in=msbyte, out=most);
    Or(a=least, b=most, out=notzr);
    Not(in=notzr, out=zr);
    
    // A number is negative in two's complement if its most significat bit is 1, thus evaluating only to 1 when ANDed with a 1. Its output is the ng control bit
    And(a=true, b=msbit, out=ng);
    
}