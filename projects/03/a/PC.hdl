// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:

    // This functionality can be achieved by multiplexing through the various conditions and finally feeding the register back in order to be processed in the next clock cycle.

    // Fed back register increment.
    Inc16(in=feedback, out=incOut);

    // Selects between the fed back register and its increment, based on the inc control bit.
    Mux16(a=feedback, b=incOut, sel=inc, out=incORnot);

    // Selects between the chip input and the previous output, based on the load control bit.
    Mux16(a=incORnot, b=in, sel=load, out=loadOut);

    // Resets or not the input, based on the reset control bit.
    Mux16(a=loadOut, b=false, sel=reset, out=resetORnot);

    // Register used to store the count.
    Register(in=resetORnot, load=true, out=feedback, out=out);

}
